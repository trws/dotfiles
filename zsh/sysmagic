#!zsh
# This file, sourced from ZDOTDIR/.zshenv, sets up $system, $host, $fqhost,
# $domain, $gotroot This is done in various (case-selected) site-specific ways,
# so make sure everything's right whenever migrating to a new system.  gotroot
# defaults to 0, which is correct on any system on which you are not root. Put
# in conditionals to set it to 1 on systems where you are root. If gotroot is
# 0, su and sudo will fail without calling the system binaries, to save you
# from any embarassment.

host=${HOST%%.*}
domain=${HOST#*.}
case $OSTYPE {
solaris*)
	system=solaris
	bintype="`bintype -os`"
	;;
linux*)
	system=linux
	bintype=$MACHTYPE-linux
	bintype=${bintype/i?86/i386} # is this wanted?
	  if [[ -e /etc/gentoo-release ]]; then distro=gentoo
	elif [[ -e /etc/debian_version ]]; then distro=debian
	fi;;
darwin*)
        system=darwin
        bintype=intel-darwin
        ;;
*)
	system=${OSTYPE%%[^a-zA-Z]*}
	bintype=${bintype/i?86/i386} # is this wanted?
	bintype=$MACHTYPE-$system
	;;
}

export SYSTEM=$system
export ARCH="$(uname -m)"
export SYS_TYPE="${SYS_TYPE:-$system-$ARCH}"
zshuse=($zshuse $system $distro site-$domain site-$host $ARCH)
typeset -U zshuse # needed because $domain may== $host

gotroot=0

roothosts=(
	abrams
	bolt
	chimera
	falcon
	nixos
)
for rh in $roothosts ; do
	if [[ $rh =~ $HOST.* ]] ; then
		gotroot=1
	fi
done
unset roothosts


# IMPORT from prezto's helper module, fantastic idea
# is true on MacOS Darwin
function is-darwin {
  [[ "$OSTYPE" == darwin* ]]
}

# is true on Linux's
function is-linux {
  [[ "$OSTYPE" == linux* ]]
}

# is true on BSD's
function is-bsd {
  [[ "$OSTYPE" == *bsd* ]]
}

# is true on Cygwin (Windows)
function is-cygwin {
  [[ "$OSTYPE" == cygwin* ]]
}
